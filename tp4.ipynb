{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "e909be72",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "# ****************************************************************************\n",
      "# FACTORISATION DES PUISSANCES\n",
      "# ****************************************************************************\n",
      "\n",
      "\n",
      "$ Question 3\n",
      "La racine de x^172 + (41*omega + 3)*x^129 + 38*omega*x^86 + (4*omega + 2)*x^43 + 42*omega + 42 est y^10 + 2*y^5 + (2*alpha + 1)*y + 1\n",
      "\n",
      "$ Question 4\n",
      "Test sur 100 exemples :  True\n"
     ]
    }
   ],
   "source": [
    "print(\"\"\"\\\n",
    "# ****************************************************************************\n",
    "# FACTORISATION DES PUISSANCES\n",
    "# ****************************************************************************\n",
    "\"\"\")\n",
    "\n",
    "\n",
    "# Donnees de l'enonce de l'exercice\n",
    "\n",
    "F1849.<omega> = FiniteField(43^2,modulus=x^2+1)\n",
    "Pol1849.<x> = PolynomialRing(F1849)\n",
    "f=x^172+(3-2*omega)*x^129-5*omega*x^86+(2 + 4*omega)*x^43-1-omega \n",
    "\n",
    "F9.<alpha> = FiniteField(9)\n",
    "Pol9.<y> = PolynomialRing(F9)\n",
    "g = y^30-y^15+alpha*y^3+1\n",
    "# Code pour l'EXERCICE\n",
    "\n",
    "def racine_p_polynome(f):\n",
    "    Pol=f.parent()\n",
    "    x=Pol.gen()\n",
    "    p=Pol.base_ring().characteristic()\n",
    "    q=Pol.base_ring().cardinality()\n",
    "    u=Pol(0)\n",
    "    coeffs = list(f)\n",
    "    coeffs = coeffs[0:f.degree()+1:p]\n",
    "    for i in range(len(coeffs)):\n",
    "        u += coeffs[i]^(q/p) * x^i\n",
    "    assert(u^p==f)\n",
    "    return u\n",
    "\n",
    "# n est le nombre de tests et Pol l'anneau de polyômes sur lequel on teste\n",
    "def testRacine(n,Pol):\n",
    "    for _ in range(n):\n",
    "        f = Pol.random_element()\n",
    "        p = Pol.characteristic()\n",
    "        fp = f^p\n",
    "        if racine_p_polynome(fp)!=f:\n",
    "            return False\n",
    "    return True\n",
    "\n",
    "test100 = testRacine(100,Pol9)\n",
    "\n",
    "# # Affichage des resultats\n",
    "\n",
    "print( \"\\n$ Question 3\")\n",
    "print( \"La racine de\",f,\"est\",racine_p_polynome(g))\n",
    "print( \"\\n$ Question 4\")\n",
    "print( \"Test sur 100 exemples : \",test100)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "54f14a8d",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "# ****************************************************************************\n",
      "# FACTORISATION SANS FACTEURS CARRES\n",
      "# ****************************************************************************\n",
      "\n",
      "\n",
      "$ Question 2\n",
      "La factorisation de x^10 + 6*x^9 + 3*x^7 + 3*x^3 + 4*x^2 + 2 est [(x + 5, 1), (x + 4, 2), (x + 3, 7)]\n",
      "\n",
      "Sage donne la factorisation : f = (x + 5) * (x + 4)^2 * (x + 3)^7\n",
      "\n",
      "$ Question 4\n",
      "Test sur 1000 exemples :  True\n"
     ]
    }
   ],
   "source": [
    "print(\"\"\"\\\n",
    "# ****************************************************************************\n",
    "# FACTORISATION SANS FACTEURS CARRES\n",
    "# ****************************************************************************\n",
    "\"\"\")\n",
    "\n",
    "# Donnees de l'enonce de l'exercice\n",
    "\n",
    "F7 = FiniteField(7)\n",
    "Pol7.<x> = PolynomialRing(F7)\n",
    "f = x^10 +6*x^9 +3*x^7 +3*x^3 +4*x^2 +2\n",
    "\n",
    "# Code pour l'EXERCICE\n",
    "\n",
    "def racine_p_polynome(f):\n",
    "    Pol=f.parent()\n",
    "    x=Pol.gen()\n",
    "    p=Pol.base_ring().characteristic()\n",
    "    q=Pol.base_ring().cardinality()\n",
    "    u=Pol(0)\n",
    "    coeffs = list(f)\n",
    "    coeffs = coeffs[0:f.degree()+1:p]\n",
    "    for i in range(len(coeffs)):\n",
    "        u += coeffs[i]^(q/p) * x^i\n",
    "    assert(u^p==f)\n",
    "    return u\n",
    "\n",
    "def myFsFC(f):\n",
    "    Pol=f.parent()\n",
    "    x=Pol.gen()\n",
    "    p=Pol.base_ring().characteristic()\n",
    "    q=Pol.base_ring().cardinality()\n",
    "    d = f.degree()\n",
    "    if d<= 0:\n",
    "        return []\n",
    "    elif f.derivative()!=0:\n",
    "        i=1\n",
    "        L=[]\n",
    "        t=gcd(f.derivative(),f)\n",
    "        u = f//t\n",
    "        while u!=Pol(1):\n",
    "            y = gcd(t,u)\n",
    "            if i%p!=0 and u//y != Pol(1) :\n",
    "                L.append((u//y,i))\n",
    "            i+=1\n",
    "            u=y\n",
    "            t=t//y\n",
    "        if t!= Pol(1) :\n",
    "            LL = myFsFC(racine_p_polynome(t))\n",
    "            for (s,j) in LL :\n",
    "                L.append((s,p*j))\n",
    "    else : \n",
    "        LL = myFsFC(racine_p_polynome(f))\n",
    "        for (s,j) in LL :\n",
    "            L.append((s,p*j))\n",
    "    assert(prod([f^e for (f,e) in L ]) == f)\n",
    "    return L\n",
    "\n",
    "# n est le nombre de tests et Pol l'anneau de polyômes sur lequel on teste\n",
    "def testFsFC(n,Pol):\n",
    "    for _ in range(n):\n",
    "        f = Pol.random_element()\n",
    "        try : #on utilise le fait qu'on teste a la fin de la fonction que la décomposition est bonne\n",
    "            myFsFC(f)\n",
    "        except AssertionError :\n",
    "            return False\n",
    "    return True\n",
    "\n",
    "test1000 = testFsFC(1000,Pol7)\n",
    "    \n",
    "# # Affichage des resultats\n",
    "\n",
    "print( \"\\n$ Question 2\")\n",
    "print( \"La factorisation de\",f,\"est\",myFsFC(f))\n",
    "print(\"\\nSage donne la factorisation : f =\", f.factor())\n",
    "print( \"\\n$ Question 4\")\n",
    "print( \"Test sur 1000 exemples : \",test1000)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "ed225b7a",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "# ****************************************************************************\n",
      "# FACTORISATION ETAGEE EN DEGRES DISTINCTS\n",
      "# ****************************************************************************\n",
      "\n",
      "\n",
      "$ Question 1\n",
      "La factorisation de x^10 + 3*x^9 + x^8 + x^7 + 4*x^6 + 3*x^5 + 2*x^4 + 2*x^3 + 4*x est [x^2 + x, x^2 + x + 2, x^6 + x^5 + x^4 + 4*x^3 + x^2 + 2*x + 2]\n"
     ]
    }
   ],
   "source": [
    "print(\"\"\"\\\n",
    "# ****************************************************************************\n",
    "# FACTORISATION ETAGEE EN DEGRES DISTINCTS\n",
    "# ****************************************************************************\n",
    "\"\"\")\n",
    "\n",
    "# Donnees de l'enonce de l'exercice\n",
    "\n",
    "F5 = FiniteField(5)\n",
    "Pol.<x>=PolynomialRing(F5)\n",
    "f = x^10-2*x^9+x^8+x^7-x^6-2*x^5+2*x^4+2*x^3-x\n",
    "\n",
    "# Code pour l'EXERCICE\n",
    "\n",
    "\n",
    "def myFEDD(f):\n",
    "    Pol=f.parent()\n",
    "    x=Pol.gen()\n",
    "    p=Pol.base_ring().characteristic()\n",
    "    q=Pol.base_ring().cardinality()\n",
    "    retour = []\n",
    "    hi=x\n",
    "    fi=f\n",
    "    i=0\n",
    "    while fi!=Pol(1):\n",
    "        hi = hi^q % f\n",
    "        gi = gcd(fi,hi-x)\n",
    "        retour.append(gi)\n",
    "        fi = fi//gi\n",
    "    assert(prod(retour) == f)\n",
    "    return retour\n",
    "\n",
    "# # Affichage des resultats\n",
    "\n",
    "print( \"\\n$ Question 1\")\n",
    "print( \"La factorisation de\",f,\"est\",myFEDD(f))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "3b0a9de9",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "# ****************************************************************************\n",
      "# CANTOR-ZASSENHAUSS\n",
      "# ****************************************************************************\n",
      "\n",
      "\n",
      "Le test pour CZ impair vaut :  True\n",
      "\n",
      "Le test pour CZ pair vaut : True\n"
     ]
    }
   ],
   "source": [
    "print(\"\"\"\\\n",
    "# ****************************************************************************\n",
    "# CANTOR-ZASSENHAUSS\n",
    "# ****************************************************************************\n",
    "\"\"\")\n",
    "\n",
    "# Donnees de l'enonce de l'exercice\n",
    "\n",
    "q=3\n",
    "d=4\n",
    "Fq=FiniteField(q)\n",
    "Polq.<x> = PolynomialRing(Fq) \n",
    "irred = [f for f in Polq.polynomials(of_degree=d) if f.is_irreducible()\n",
    " and f.leading_coefficient()==1]\n",
    "\n",
    "# Code pour l'EXERCICE\n",
    "\n",
    "def random_product(L):\n",
    "    res= []\n",
    "    LL=L.copy()\n",
    "    k = randint(1,len(LL)-1) #nombre de termes du produit\n",
    "    while k>0 :\n",
    "        i = randint(0,len(LL)-1)\n",
    "        res.append(LL.pop(i))\n",
    "        k-=1\n",
    "    return prod(res)\n",
    "\n",
    "# n nombre de tests, d degré, irred irreductibles de ce degré, carac la caractéristique\n",
    "def testCZ(carac,n,d,irred):\n",
    "    for i in range(n):\n",
    "        f = random_product(irred)\n",
    "        try :\n",
    "            if carac%2==0:\n",
    "                myCZ2(f,d)\n",
    "            else :\n",
    "                myCZ(f,d)\n",
    "        except AssertionError : \n",
    "            return False\n",
    "    return True\n",
    "\n",
    "def myCZ(f,d):\n",
    "    Pol=f.parent()\n",
    "    x=Pol.gen()\n",
    "    p=Pol.base_ring().characteristic()\n",
    "    q=Pol.base_ring().cardinality()\n",
    "    retour = []\n",
    "    if f.degree()==d:\n",
    "        retour.append(f)\n",
    "        return retour\n",
    "    else :\n",
    "        u = Pol.random_element((0,2*d-1))\n",
    "        b = gcd(f,u^((q^d-1)/2)-1)\n",
    "        while 0 >= b.degree() or b.degree() >= f.degree() :\n",
    "            u = Pol.random_element((0,2*d-1))\n",
    "            b = gcd(f,u^((q^d-1)/2)-1)\n",
    "    r1 = myCZ(b,d)\n",
    "    r2 = myCZ(f//b,d)\n",
    "    retour = r1+r2\n",
    "    assert(prod(retour) == f)\n",
    "    return retour\n",
    "\n",
    "testCZ20 = testCZ(q,20,d,irred)\n",
    "\n",
    "q=2\n",
    "d=4\n",
    "Fq=FiniteField(q)\n",
    "Polq.<x> = PolynomialRing(Fq) \n",
    "irred = [f for f in Polq.polynomials(of_degree=d) if f.is_irreducible()\n",
    " and f.leading_coefficient()==1]\n",
    "\n",
    "\n",
    "def myCZ2(f,d):\n",
    "    Pol=f.parent()\n",
    "    x=Pol.gen()\n",
    "    p=Pol.base_ring().characteristic()\n",
    "    q=Pol.base_ring().cardinality()\n",
    "    retour = []\n",
    "    k = log(q,2)\n",
    "    if f.degree()==d:\n",
    "        retour.append(f)\n",
    "        return retour\n",
    "    else :\n",
    "        tr = sum(x^(2^i) for i in range(k*d))\n",
    "        u = Pol.random_element((0,2*d-1))\n",
    "        b = gcd(f, tr(u))\n",
    "        while 0>= b.degree() or b.degree() >= f.degree() :\n",
    "            u = Pol.random_element((0,2*d-1))\n",
    "            b = gcd(f, tr(u))\n",
    "    r1 = myCZ2(b,d)\n",
    "    r2 = myCZ2(f//b,d)\n",
    "    retour = r1+r2\n",
    "    assert(prod(retour) == f)\n",
    "    return retour    \n",
    "\n",
    "testCZ220 = testCZ(q,20,d,irred)\n",
    "# # Affichage des resultats\n",
    "print(\"\\nLe test pour CZ impair vaut : \", testCZ20)\n",
    "print(\"\\nLe test pour CZ pair vaut :\", testCZ220)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 9,
   "id": "ad00cd15",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "# ****************************************************************************\n",
      "# FACTORISATION COMPLETE\n",
      "# ****************************************************************************\n",
      "\n",
      "\n",
      "$ Question 1\n",
      "La factorisation de x^86 + x^85 + x^84 + 2*x^83 + 2*x^82 + 2*x^81 + 2*x^80 + x^78 + 2*x^77 + 2*x^74 + x^71 + x^70 + 2*x^69 + 2*x^67 + 2*x^66 + 2*x^65 + 2*x^63 + x^61 + x^60 + 2*x^57 + x^56 + x^53 + x^51 + x^50 + 2*x^48 + x^46 + x^44 + 2*x^42 + 2*x^41 + x^40 + 2*x^38 + 2*x^37 + x^36 + 2*x^33 + 2*x^32 + x^31 + x^30 + x^29 + 2*x^26 + 2*x^24 + 2*x^22 + 2*x^20 + 2*x^19 + x^18 + x^17 + x^16 + 2*x^15 + x^13 + x^12 + 2*x^11 + 2*x^10 + 2*x^9 + x^8 + x^5 + 2*x^4 + x^2 + 2*x est\n",
      " [(x, 1), (x^2 + 1, 1), (x^3 + 2*x + 1, 1), (x^3 + 2*x + 2, 1), (x^3 + x^2 + x + 2, 2), (x^3 + x^2 + 2, 2), (x + 2, 4), (x^2 + x + 2, 4), (x^2 + 2*x + 2, 4), (x^3 + 2*x^2 + x + 1, 4), (x^3 + 2*x^2 + 2*x + 2, 4), (x + 1, 3), (x^3 + x^2 + 2*x + 1, 3), (x^3 + 2*x^2 + 1, 3)] \n",
      "Avec le coefficient dominant : 1\n"
     ]
    }
   ],
   "source": [
    "print(\"\"\"\\\n",
    "# ****************************************************************************\n",
    "# FACTORISATION COMPLETE\n",
    "# ****************************************************************************\n",
    "\"\"\")\n",
    "\n",
    "\n",
    "# Donnees de l'enonce de l'exercice\n",
    "\n",
    "q=3\n",
    "Fq=FiniteField(q)\n",
    "Polq.<x> = PolynomialRing(Fq) \n",
    "L1 = [f for f in Polq.polynomials(of_degree=1) if f.is_irreducible()\n",
    "and f.leading_coefficient()==1]\n",
    "L2 = [f for f in Polq.polynomials(of_degree=2) if f.is_irreducible()\n",
    "and f.leading_coefficient()==1]\n",
    "L3 = [f for f in Polq.polynomials(of_degree=3) if f.is_irreducible()\n",
    "and f.leading_coefficient()==1]\n",
    "    \n",
    "f = L1[0]*L1[1]^3*L1[2]^4\n",
    "f *= L2[0]*L2[1]^4*L2[2]^4\n",
    "f *= L3[0]*L3[1]*L3[2]^2*L3[3]^2*L3[4]^3*L3[5]^3*L3[6]^4*L3[7]^4\n",
    "factor(f)\n",
    "\n",
    "\n",
    "# Code pour l'EXERCICE\n",
    "\n",
    "def myFactorisation(f):\n",
    "    Pol=f.parent()\n",
    "    x=Pol.gen()\n",
    "    p=Pol.base_ring().characteristic()\n",
    "    q=Pol.base_ring().cardinality()\n",
    "    retour = []\n",
    "    alpha = list(f)[-1] #coeff en facteur\n",
    "    f = f/alpha\n",
    "    f_sans_carre = myFsFC(f)\n",
    "    for (g,i) in f_sans_carre :\n",
    "        g_deg_distinct = myFEDD(g)\n",
    "        for j in range(len(g_deg_distinct)) :\n",
    "            if g_deg_distinct[j]!= Pol(1) :\n",
    "                if p%2 == 0:\n",
    "                    irred = myCZ2(g_deg_distinct[j],j+1)\n",
    "                else :\n",
    "                    irred = myCZ(g_deg_distinct[j],j+1)\n",
    "                for u in irred :\n",
    "                    retour.append((u,i))\n",
    "    assert(alpha*prod([f^e for (f,e) in retour ]) == f)\n",
    "    return (retour, alpha)\n",
    "\n",
    "\n",
    "\n",
    "# # Affichage des resultats\n",
    "\n",
    "print( \"\\n$ Question 1\")\n",
    "facto, alpha = myFactorisation(f)\n",
    "print( \"La factorisation de\",f,\"est\\n\",facto, \"\\nAvec le coefficient dominant :\", alpha )"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 10,
   "id": "b1705615",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "# ****************************************************************************\n",
      "# RACINES D'UN POLYNOME\n",
      "# ****************************************************************************\n",
      "\n",
      "\n",
      "$ Question 1\n",
      "Les racines de  x^86 + x^85 + x^84 + 2*x^83 + 2*x^82 + 2*x^81 + 2*x^80 + x^78 + 2*x^77 + 2*x^74 + x^71 + x^70 + 2*x^69 + 2*x^67 + 2*x^66 + 2*x^65 + 2*x^63 + x^61 + x^60 + 2*x^57 + x^56 + x^53 + x^51 + x^50 + 2*x^48 + x^46 + x^44 + 2*x^42 + 2*x^41 + x^40 + 2*x^38 + 2*x^37 + x^36 + 2*x^33 + 2*x^32 + x^31 + x^30 + x^29 + 2*x^26 + 2*x^24 + 2*x^22 + 2*x^20 + 2*x^19 + x^18 + x^17 + x^16 + 2*x^15 + x^13 + x^12 + 2*x^11 + 2*x^10 + 2*x^9 + x^8 + x^5 + 2*x^4 + x^2 + 2*x sont [0, 1, 2] [(0, 1), (2, 3), (1, 4)]\n"
     ]
    }
   ],
   "source": [
    "print(\"\"\"\\\n",
    "# ****************************************************************************\n",
    "# RACINES D'UN POLYNOME\n",
    "# ****************************************************************************\n",
    "\"\"\")\n",
    "\n",
    "# Donnees de l'enonce de l'exercice\n",
    "\n",
    "q=3\n",
    "Fq=FiniteField(q)\n",
    "Polq.<x> = PolynomialRing(Fq) \n",
    "L1 = [f for f in Polq.polynomials(of_degree=1) if f.is_irreducible()\n",
    "and f.leading_coefficient()==1]\n",
    "L2 = [f for f in Polq.polynomials(of_degree=2) if f.is_irreducible()\n",
    "and f.leading_coefficient()==1]\n",
    "L3 = [f for f in Polq.polynomials(of_degree=3) if f.is_irreducible()\n",
    "and f.leading_coefficient()==1]\n",
    "    \n",
    "f = L1[0]*L1[1]^3*L1[2]^4\n",
    "f *= L2[0]*L2[1]^4*L2[2]^4\n",
    "f *= L3[0]*L3[1]*L3[2]^2*L3[3]^2*L3[4]^3*L3[5]^3*L3[6]^4*L3[7]^4\n",
    "\n",
    "\n",
    "# Code pour l'EXERCICE\n",
    "\n",
    "\n",
    "def myRacine(f):\n",
    "    Pol=f.parent()\n",
    "    x=Pol.gen()\n",
    "    p=Pol.base_ring().characteristic()\n",
    "    q=Pol.base_ring().cardinality()\n",
    "    retour = []\n",
    "    facto, alpha = myFactorisation(f)\n",
    "    for (irr, puissance) in facto :\n",
    "        if irr.degree() == 1 :\n",
    "            root = Pol.base_ring()(-(irr - x))\n",
    "            retour.append(root)\n",
    "    assert(f(z)==0 for z in retour)\n",
    "    return retour\n",
    "\n",
    "# # Affichage des resultats\n",
    "\n",
    "print( \"\\n$ Question 1\")\n",
    "print( \"Les racines de \",f,\"sont\",myRacine(f))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f4c361a7",
   "metadata": {},
   "outputs": [],
   "source": [
    "print(\"\"\"\\\n",
    "# ****************************************************************************\n",
    "# ETUDE DE CANTOR-ZASSENHAUSS\n",
    "# ****************************************************************************\n",
    "\"\"\")\n",
    "\n",
    "# Donnees de l'enonce de l'exercice\n",
    "\n",
    "# Code pour l'EXERCICE\n",
    "\n",
    "\n",
    "# # Affichage des resultats\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SageMath 9.7",
   "language": "sage",
   "name": "sagemath"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.10.8"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
